% a sample grammar to reveal LFs of examples requiring BTSO -- cem bozsahin

% O/D test 

i n  := s/(s\np[agr=1s]) : \p. p !i;
you n  := s/(s\np[agr=2s]) : \p. p !you;
know v := (s\np)/^s: \p\x. !know p x;
what wh := s/^(s/np) : \p\x. !what (p x);
can aux := (s\np)/^(s\np): \p\x. !can (p x);
should aux := (s\np)/^(s\np): \p\x. !shall (p x);
not aux := (s\np)/(s\np): \p\x. !not (p x);
do v    := (s\^np)/^np: \x\y. !do x y;
and x  := (@X\*@X)/*@X: \p\q\x. !and (p x)(q x);

%now for CL book's proposed cats at p.169

think v := (s\np)/sp : \p\x. !think p x;
claims v := (s\np[agr=3s])/sp : \p\x. !claim p x;
who wh := (N\N)/(S\NP) : \p\q\x. !and (p x)(q x);
barry n  := s/(s\np[agr=3s]) : \p. p !barry;
owns v := (sp\np)/np : \x\y.!own x y;   
owns v := (s\np)/np : \x\y.!own x y;
the n := (s/(s\np))/*n : \x\p. p x;            %matrix subj
the n := (s\(s/np))/*n : \x\p. p x;            %matrix obj
the n := ((sp\np)\((sp\np)/np))/*n : \x\p. p x;  %subordinate obj
house n := n : \x. !house x;
book n := n : \x. !book x;
man n := n : \x. !man x;

% S test: parasitic gap

which wh := (N\N)/(S/NP) : \p\q\x. !and (p x)(q x);
read v := (s\np)/np: \x\y. !read x y;
understanding ger := c[type=ing]/np: \x\y.!understand x y;
without p := ((s\np)\(s\np))/c[type=ing] : \p\q\x. !without (p x) (q x);
burns v := s\np[agr=3s]: \x.!burn x;

% B powers test: Turkish causative

ver v := s\np[case=nom]\np[case=dat]\np[case=acc]: \x\y\z. !give y x z;
-dir aff := (s\np[case=nom]\np[case=?c])\(s\np[case=nom]) 
: \p\x\y. !cause (p x) y;
-t aff := (s\np[case=nom]\np[case=?c])\(s\np[case=nom]) 
: \p\x\y. !cause (p x) y;

% Joshi/Henderson test for Ken and Barbie said that Ken; cf. Henderson's 1990 ms thesis.
% Even with most liberal modalities as below, there will be no 
% '(Ken and Barbie said that Ken) owns the house'
% constituent which does not have (Ken and Barbie) constituent first.
% In cases where we have (Barbie said that Ken), 'owns' is always the
% main predicate, i.e. (and Barbie said that Ken) is a parenthetical.
% Check the LFs with (Ken and Barbie) constituency. 'owns' is never
% the main predicate.
% O and s-prime (sp) subtyping of the verbs do it. see owns above.

ken n := np : !ken;
barbie n := np : !barbie;
(mtr) np : lf --> s/(s\np) : \lf\p. p lf;  % matrix type raise
(etr) np : lf --> sp/(sp\np) : \lf\p. p lf; %embedded clause tr
said v := (s\np)/sp: \p\x. !say p x;
that c := sp/s : \p. p;
(mtr-o) np : lf --> (s\np)\((s\np)/np) : \lf\p. p lf;  % matrix type raise obj
(etr-o) np : lf --> (sp\np)\((sp\np)/np) : \lf\p. p lf; %embedded clause tr obj

